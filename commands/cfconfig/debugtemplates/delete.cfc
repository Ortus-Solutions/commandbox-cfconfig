/**
* Delete a debug template.  Identify the debug template uniquely by the label.
* 
* {code}
* cfconfig debugtemplates delete myDebugTemplate
* cfconfig debugtemplates delete myDebugTemplate serverName
* cfconfig debugtemplates delete myDebugTemplate /path/to/server/home
* {code}
*
*/
component {
	
	property name='CFConfigService' inject='CFConfigService@cfconfig-services';
	property name='Util' inject='util@commandbox-cfconfig';
	property name="serverService" inject="ServerService";
	/**
	* @label The name of the debug template
	* @to CommandBox server name, server home path, or CFConfig JSON file. Defaults to CommandBox server in CWD.
	* @to.optionsFileComplete true
	* @to.optionsUDF serverNameComplete
	* @toFormat The format to write to. Ex: LuceeServer@5
	*/	
	function run(
		required string label,
		string to,
		string toFormat
	) {		
		var to = arguments.to ?: '';
		var toFormat = arguments.toFormat ?: '';

		try {
			var toDetails = Util.resolveServerDetails( to, toFormat, 'to' );
		} catch( cfconfigException var e ) {
			error( e.message, e.detail ?: '' );
		}
			
		if( !toDetails.path.len() ) {
			error( "The location for the server couldn't be determined.  Please check your spelling." );
		}
		
		// Read existing config
		var oConfig = CFConfigService.determineProvider( toDetails.format, toDetails.version )
			.read( toDetails.path );

		// Get the caches and remove the requested one
		var templates = oConfig.getDebuggingTemplates() ?: {};
		templates.delete( label );	

		print.redLine(templates);
		
		// Set remaining caches back and save
		oConfig.setDebuggingTemplates( templates )
			.write( toDetails.path );		
			
		print.greenLine( 'debug template [#label#] deleted.' );
	}
	
	function serverNameComplete() {
		return serverService
			.getServerNames()
			.map( function( i ) {
				return { name : i, group : 'Server Names' };
			} );
	}
	
}